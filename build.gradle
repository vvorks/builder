plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "de.esoco.gwt" version "1.1.0"
	id "ca.coglinc.javacc" version "2.4.0"
	id "de.undercouch.download" version "5.0.1"
}

apply plugin: 'eclipse'

group = 'com.github.vvorks.builder'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

compileJjtree {
    inputDirectory = file('src/main/java')
}

sourceSets {
 	main {
 		java {
			srcDir compileJjtree.outputDirectory
			srcDir compileJavacc.outputDirectory
		}
		resources {
			srcDir 'src/main/java'
		}
	}
}

javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

gwt {
	/** Module to compile, can be multiple */
	module 'com.github.vvorks.builder.Builder'
	/** GWT version */
	gwtVersion = '2.10.0'
	/** Add the gwt-servlet lib */
	gwtServletLib = false
	/** Add the gwt-elemental lib */
	gwtElementalLib = false
	/** Add GWT plugin config (only if plugin 'eclipse' is enabled) */
	gwtPluginEclipse = false
	/** GWT compile options */
	compile {
		/** The level of logging detail (ERROR, WARN, INFO, TRACE, DEBUG, SPAM, ALL) */
		logLevel = "INFO"
		/** Script output style. (OBF, PRETTY, DETAILED)*/
		style = "PRETTY"
		/** Sets the optimization level used by the compiler. 0=none 9=maximum. */
		optimize = 5
		/** avoid classpath too long problem */
		envClasspath = true
	}
	dev {
		/** avoid classpath too long problem */
		envClasspath = true
	}
}

task copyGWTCode(dependsOn: ["gwtCompile"], type: Copy) {
	from file("${buildDir}/gwt/out")
	into file("${buildDir}/resources/main/static")
}

bootJar {
	dependsOn copyGWTCode
	archiveName = "vvorksbuilder-${version}.jar"
}

bootRun {
	dependsOn copyGWTCode
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	compileOnly 'org.fusesource.restygwt:restygwt:2.2.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'junit:junit:4.13'
	implementation 'org.apache.poi:poi:5.1.0'
	implementation 'org.apache.poi:poi-ooxml:5.1.0'
	implementation 'com.github.jknack:handlebars:4.2.0'
	implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'
	implementation 'com.google.googlejavaformat:google-java-format:1.13.0'
}

test {
	useJUnitPlatform()
}
